+++
title = "Polymorphism"
author = ["Kiran"]
date = 2024-07-31T10:26:00-04:00
tags = ["sv"]
draft = false
css = "../../zcustom.css"
+++

[Source](https://github.com/24x7fpga/SystemVerilog_Verification/blob/main/sv_verification/polymorphism/tb_polymorphism.sv) -- [SV Verification Directory]({{< relref "2024_06_27_16_53_00_sv_verification_directory.md" >}})


## Polymorphism {#polymorphism}

As the term 'polymorphism' implies, 'poly' means 'many' and 'morph' means 'form' or 'shape'. A base class handle can invoke methods of its child class which has the same name. Hence, an object can take many forms.

-   An extended class object can be assigned to the base class but not the other way round.
-   [Virtual Keyword]({{< relref "2024_07_31_10_21_04_virtual_keyword.md" >}}) should be used in parent class to override the method by the child class.


#### Example Code {#example-code}

```verilog
// Parent class
class parent_class;
   // class properties or variables
  int a, b, s;

   // task method
  task func1 (int a, b);
    s      = a + b;
    $display("Non-Virtual: Parent Class => a = %0d , b = %0d, sum s = %0d", a, b, s);
  endtask

  virtual task func2 (int a, b);
    s      = a + b;
    $display("Virtual:     Parent Class => a = %0d , b = %0d, sum s = %0d", a, b, s);
  endtask

endclass

// Child Class
class child_class extends parent_class;
  // class properties or variables
  int a, b, m;

  // task method
  task func1 (int a, b);
    m      = a * b;
    $display("Non-Virtual: Child Class  => a = %0d , b = %0d, mul m = %0d", a, b, m);
  endtask

  virtual task func2 (int a, b); // good practice to declare a class method as virtual
    m      = a * b;
    $display("Virtual:     Child Class  => a = %0d , b = %0d, mul m = %0d", a, b, m);
  endtask

endclass

module tb_polymorphism;

  parent_class pr_obj;          // declare a handle for parent class
  child_class cl_obj;           // declare a handle for child class

  task run(parent_class arg, int a, b);
    arg.func1(a, b);            // commented and uncomment to
    arg.func2(a, b);            // see the effects
  endtask

   initial begin
     cl_obj = new( );           // construct an object

     pr_obj = new( );           // WHEN THE CHILD CLASS HANDLE POINTS TO THE PARENT CLASS HANDLE

     $display("---------------------- Polymorphism ----------------------");
     run(pr_obj, 30, 10);
     run(cl_obj, 30, 10);

   end

endmodule
```

[Execute the code in EDA Playground](https://www.edaplayground.com/x/vU4b)

In the example, the task `run` behaves differently each time it is called, depending on the argument passed to it. This behavior is known as _polymorphism_, and it is achieved by combining the behaviour of the class pointer and inheritance. When the task `run` is called, an argument is created, and based on this argument, the task's pointer can reference either the base or the child class. This works because the base and child classes are of the same type, meaning the child class inherits all the characteristics of the base class. However, a potential issue arises when both the base and child classes define a method with the same name. In the example above, both classes contain `func1` and `func2`. When executing the child class, `func1` from the base class is executed instead of the childâ€™s implementation. This behavior defeats the purpose of _polymorphism_. To ensure that the child class's method is executed, the `virtual` keyword is prefixed to the function declaration. In the example, the function `func2` executes as intended, demonstrating proper _polymorphism_.

**Note**: It is not permissible to assign a parent class handle to a child class handle directly. To address this issue, [Dynamic Cast]({{< relref "2024_07_02_18_41_56_dynamic_cast.md" >}})ing is employed.
